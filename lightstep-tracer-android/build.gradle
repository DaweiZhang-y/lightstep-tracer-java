// The library versions are kept in sync
version = new File('../common/VERSION').text
def current_version = version

// This sets up the dependencies for the build script *itself*, not the package.
buildscript {
    // Work around a defect in the 'android-library' plugin's version check
    System.properties['com.android.build.gradle.overrideVersionCheck'] = 'true'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'com.github.dcendents:android-maven-plugin:1.2'
    }
}


apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// Make sure the library is Java 7 compatible
targetCompatibility = '1.7'
sourceCompatibility = '1.7'

group = "com.lightstep.tracer"

def siteUrl = 'https://github.com/lightstep/lightstep-tracer-java'
def gitUrl = 'https://github.com/lightstep/lightstep-tracer-java.git'

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"
    resourcePrefix "lightstep__"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 2
        versionName version
    }
    buildTypes {
    }
}

Properties properties = new Properties()

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "lightstep-tracer-android"
        userOrg = 'lightstep'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publicDownloadNumbers = true
        publish = true
        version {
            name = current_version
            released = new Date()
        }
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'The LightStep OpenTracing Implementation for Android'
                url siteUrl
                developers {
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

repositories {
    jcenter()
    maven {
        // published beta artifacts of opentracing API
        url  "http://oss.jfrog.org/oss-snapshot-local/"
    }
}

dependencies {
    compile 'org.apache.thrift:libthrift:0.9.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'org.json:json:20160212'
    compile 'io.opentracing:opentracing-api:0.1.0-SNAPSHOT'


    // TODO: can we remove this awkward dependency on the JRE JAR?
    compile files('../lightstep-tracer-jre/build/libs/lightstep-tracer-jre-' + current_version + '.jar')
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
