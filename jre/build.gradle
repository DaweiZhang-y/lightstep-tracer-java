
// This sets up the dependencies for the build script *itself*, not the package.
// E.g. the classpath for the plugins
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

// Include...
// - java plugin to build the jar
// - the bintray plugin to publish the jar to bintra
//
apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'

jar {
    baseName 'lightstep-tracer-jre'
    version '0.1.0'
}

// By default, only "src/" will be included, but we want to include the code
// from the "common" project as well -- and want it to be part of this JAR,
// not a separate artifact.
sourceSets {
    main {
        java {
            srcDir "src/"
            srcDir "../common/src"
        }
    }
}

// Defines the repositories need for dependencies below
repositories {
    jcenter()
}

// Dependencies to build this JAR archive
dependencies {
    compile 'org.apache.thrift:libthrift:0.9.2'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.3'
    compile 'org.json:json:20160212'
}

// Bintray configuration so we can publish publically.  Requires the environment
// variables below be set.
// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'lightstep-tracer-jre'
        userOrg = 'lightstep'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/lightstep/lightstep-tracer-java.git'
        publicDownloadNumbers = true

        version {
            name = '0.1.0'
            released  = new Date()
        }
    }
}
